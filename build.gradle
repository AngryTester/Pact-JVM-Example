buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
        classpath("org.thymeleaf:thymeleaf:3.0.8.RELEASE")
        classpath("au.com.dius:pact-jvm-provider-gradle_2.12:3.5.10")
    }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'

  repositories {
     mavenCentral()
  }
}

subprojects {
  apply plugin: 'org.springframework.boot'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-devtools")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("junit:junit")
    testCompile("au.com.dius:pact-jvm-consumer-junit_2.12:3.5.9")
    testCompile("au.com.dius:pact-jvm-consumer-java8_2.12:3.5.9")
  }

}

project(':example-consumer-miku') {
    jar {
        baseName = 'example-consumer-miku'
        version =  '1.0.0'
    }

    test {
        systemProperties['pact.rootDir'] = "$rootDir/Pacts/Miku"
    }
}


project(':example-consumer-nanoha') {
    jar {
        baseName = 'example-consumer-nanoha'
        version =  '1.0.0'
    }

    test {
        systemProperties['pact.rootDir'] = "$rootDir/Pacts/Nanoha"
    }
}

import java.net.URL

project(':example-provider') {
    apply plugin: 'au.com.dius.pact'

    jar {
        baseName = 'example-provider'
        version =  '1.0.0'
    }

    pact {
        serviceProviders {
            ExampleProvider {
                protocol = 'http'
                host = 'localhost'
                port = 8080
                path = '/'

                hasPactWith('Miku - Base contract') {
                    pactSource = file("$rootDir/Pacts/Miku/BaseConsumer-ExampleProvider.json")
                }

                hasPactsWith('Miku - All contracts') {
                    pactFileLocation = file("$rootDir/Pacts/Miku")
                }

                hasPactWith('Nanoha - With Nantionality') {
                    pactSource = file("$rootDir/Pacts/Nanoha/ConsumerNanohaWithNationality-ExampleProvider.json")
                }

                hasPactWith('Nanoha - No Nantionality') {
                    stateChangeUrl = new URL('http://localhost:8080/pactStateChange')
                    pactSource = file("$rootDir/Pacts/Nanoha/ConsumerNanohaNoNationality-ExampleProvider.json")
                }
            }
        }

//        reports {
//            defaultReports()
//            markdown
//            json
//        }
    }

}



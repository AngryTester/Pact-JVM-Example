group 'ariman'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
        classpath("org.thymeleaf:thymeleaf:3.0.8.RELEASE")
        classpath("au.com.dius:pact-jvm-provider-gradle_2.12:3.5.10")
    }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'

  repositories {
     mavenCentral()
  }
}

subprojects {
  apply plugin: 'org.springframework.boot'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-devtools")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile("junit:junit")
    testCompile("au.com.dius:pact-jvm-consumer-junit_2.12:3.5.9")
    testCompile("au.com.dius:pact-jvm-consumer-java8_2.12:3.5.9")
  }

}

project(':pact-jvm-example-consumer') {
    jar {
        baseName = 'pact-jvm-example-consumer'
        version =  '1.0.0'
    }

    test {
        systemProperties['pact.rootDir'] = "$rootDir/Pacts"
    }
}

project(':pact-jvm-example-provider') {
    apply plugin: 'au.com.dius.pact'

    jar {
        baseName = 'pact-jvm-example-provider'
        version =  '1.0.0'
    }

    pact {
        serviceProviders {
            ExampleProvider {
                protocol = 'http'
                host = 'localhost'
                port = 8080
                path = '/'

                hasPactWith('BaseConsumer') {
                    pactSource = file("$rootDir/Pacts/BaseConsumer-ExampleProvider.json")
                }

                hasPactsWith('AllConsumers') {
                    pactFileLocation = file("$rootDir/Pacts")
                }
            }
        }

//        reports {
//            defaultReports()
//            markdown
//            json
//        }
    }

}


